prototype(TwelveInch.Site:Content.Hero) < prototype(Neos.Neos:ContentComponent) {
    # if the current node has previous siblings it is not the
	# first content and should be loaded lazy otherwise not
	lazy = ${q(node).prev().is()}

    src = Neos.Fusion:Case {
        image {
            condition = ${q(node).property('image') ? true : false}
            renderer = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(node).property('image')}
            }
        }
        dummyImage {
            condition = ${site.context.inBackend}
            renderer = Sitegeist.Kaleidoscope:DummyImageSource
        }
    }
    alternativeText = ${q(node).property('alternativeText')}

    text = Neos.Neos:Editable {
        property = 'text'
    }
    btntext = Neos.Neos:Editable {
        property = 'btntext'
    }
    label = Neos.Neos:Editable {
        property = 'label'
    }
    link = ${q(node).property('link')}
    linkTarget = ${q(node).property('linkTarget') ? '_blank' : '_self'}
    renderAsLink = ${site.context.inBackend ? false : true}

    arrowUri = Neos.Fusion:ResourceUri {
    path = 'resource://TwelveInch.Site/Public/Frontend/svg/down-arrow-white.svg'
    }

    renderer = afx`
    <div class="hero-content">
        <div class="hero-image">
            <Sitegeist.Lazybones:Image
                class="lazy-hero"
                lazy={true}
                lazyWidth={200}
                imageSource={props.src}
                srcset="320w, 400w, 480w, 600w, 640w, 720w, 768w, 800w, 960w, 1024px, 1280w, 1440w, 1600w, 1680w, 1920w, 2560w"
                sizes={props.sizes}
                alt={props.alternativeText}
                />
        </div>
        <div class="hero-cta">
            <div class="hero-headline" data-aos="intro" data-aos-delay="400">{props.text}</div>
            <div class="" data-aos="intro" data-aos-delay="600">
                <Carbon.Link:Link link={props.link} additionalClass="hero-button" role="button">{props.label}<img src={props.arrowUri} alt=""/></Carbon.Link:Link>
            </div>
        </div>
    </div>
    `
}
